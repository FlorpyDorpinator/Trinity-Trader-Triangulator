# ============================================================================
# TRINITY TRADER TRIANGULATOR v1.8
# Main search and triangulation controller
# Uses 3-shot measurement pattern with adaptive offsets for accurate trader location
#Changelog
#This version will use a smaller v increment to allow for more precise scanning
# during the spiral scan phase. This should help in locating traders that might 
# be missed with larger increments.
# ============================================================================

# Link to solver chips via IC housing
lbn r14 HASH("2037291645") HASH("TrinityAngleSolver") ReferenceId 1
put db 100 r14  # Store reference for later use
define AngleSolver r14
lbn r14 HASH("2037291645") HASH("TrinityPolynomial") ReferenceId 1
define PolynomialSolver r14
lbn r14 HASH("2037291645") HASH("TrinityOne") ReferenceId 1
define TrinityOne r14

#0 Is Close 1 is Med and 2 is far tier Trader
Init_Preload:
# ============================================================================
# REGISTER ALIASES - Make code readable
# ============================================================================
alias TempStore r0                      # Temporary value storage
alias SelectedTrader r1                 # Trader tier to search for (0=Close, 1=Med, 2=Far)
alias CurrentTraderLock r2              # Currently detected trader tier from dish
alias HorizontalAngleValue r3           # Current horizontal angle (0-360°)
alias VerticalAngleValue r4             # Current vertical angle (0-90°)
alias TraderID r5                       # Trader contact ID (unused in this version)
alias FinalHorizontalAngleValue r6      # Final calculated H angle from solver
alias FinalVerticalAngleValue r7        # Final calculated V angle from solver
alias SmallSatRefID r9                  # Small satellite dish reference (unused)
alias MedSatRefID r10                   # Medium satellite dish reference
alias LargeSatRefID r11                 # Large satellite dish reference (unused)

# ============================================================================
# DEVICE DEFINITIONS - Hash names for devices
# ============================================================================
move SelectedTrader 2                   # Default to Far tier (2) traders
define MedSat HASH("StructureSatelliteDish")
define LargeSat HASH("StructureLargeSatelliteDish")
define TraderSelectDial HASH("StructureLogicDial")
define StartButton HASH("StructureLogicButton")
define SmallLED HASH("StructureConsoleLED5")

# ============================================================================
# INITIAL SETUP - Configure devices
# ============================================================================
sb TraderSelectDial Mode 2              # Set dial to mode 2 (0-2 range for trader tiers)
lb MedSatRefID MedSat ReferenceId Minimum   # Load medium dish reference
lb LargeSatRefID LargeSat ReferenceId Minimum  # Load large dish reference
sbn SmallLED HASH("CTL") Mode 0         # Configure LED display
sbn SmallLED HASH("CTL") Setting 0      # Set LED to 0 initially
sbn SmallLED HASH("CTID") Mode 0        # Configure LED for trader ID display

# ============================================================================
# RESET DISH - Return to starting position
# ============================================================================
ResetDish:
    s MedSatRefID Horizontal 0          # Point to H=0° (north)
    s MedSatRefID Vertical 0            # Point to V=0° (horizon)
    s MedSatRefID Setting 500           # Set power to 500W for scanning
    s MedSatRefID BestContactFilter -1  # Disable contact filtering (see all traders)
    move HorizontalAngleValue 0         # Reset H tracking variable
    move VerticalAngleValue 0           # Reset V tracking variable
jal DishMovementYield                   # Wait for dish to reach position
put MedSatRefID 0 TraderInstruction.WriteTraderData
# ============================================================================
# WAIT FOR START BUTTON
# ============================================================================
WaitForInit:
lb TempStore StartButton Activate Maximum  # Check if start button pressed
beq TempStore 0 WaitForInit             # Loop until button pressed

# ============================================================================
# SPIRAL SCAN - Search for trader using Archimedean spiral pattern
# Scans from V=0° to V=90° in 0.9° H × 0.06875° V increments
# ============================================================================
StartSpiralScan:
    yield                               # Allow other processes to run
    s MedSatRefID Horizontal HorizontalAngleValue  # Set dish H position
    s MedSatRefID Vertical VerticalAngleValue      # Set dish V position
    jal UpdateCurrentStrongestSignal    # Check for trader signal
    beq CurrentTraderLock SelectedTrader InitThreeShotProcedure  # Found it!
    add HorizontalAngleValue 0.9 HorizontalAngleValue  # Increment H by 0.9°
    add VerticalAngleValue 0.055 VerticalAngleValue  # Increment V by 0.06875°
    blt VerticalAngleValue 90 StartSpiralScan  # Continue if V < 90°
    j ResetDish                         # Reached top, reset and start over

# ============================================================================
# THREE-SHOT TRIANGULATION PROCEDURE
# Takes 3 measurements in a triangle pattern to calculate exact position
# Uses adaptive offsets: larger for low V, smaller near pole (V>70°)
# ============================================================================
InitThreeShotProcedure:
    s MedSatRefID Setting 7500          # Increase power to 7500W for precise measurements
    add HorizontalAngleValue HorizontalAngleValue 1  # Move +1° H from lock position
    s MedSatRefID Horizontal HorizontalAngleValue    # Apply position
    jal DishMovementYield               # Wait for dish movement
    sleep 25                            # Wait for signal to stabilize
    l r12 MedSatRefID WattsReachingContact  # Check if still in contact
    beq r12 -1 IterateForward           # Lost contact? Iterate forward
    jal DataGatherThreeShot             # SHOT 1: Gather first measurement
    
    # Adaptive offset selection based on vertical angle
    bgt VerticalAngleValue 70 UseSmallOffsets  # Use small offsets if V > 70°
    
    # LARGE OFFSETS (for V ≤ 70°) - Creates proper triangle: 8°H × 8°V spread
    # Pattern: Shot 1 (H+1,V), Shot 2 (H+9,V+4), Shot 3 (H+5,V+8)
    # Ensures good geometry with spread in both dimensions
    add HorizontalAngleValue HorizontalAngleValue 8   # +8° H (right)
    add VerticalAngleValue VerticalAngleValue 4       # +4° V (up)
    min VerticalAngleValue VerticalAngleValue 90      # Clamp at V=90°
    s MedSatRefID Horizontal HorizontalAngleValue
    s MedSatRefID Vertical VerticalAngleValue
    jal DishMovementYield               # Wait for movement
    sleep 25                            # Wait for signal stabilization
    jal DataGatherThreeShot             # SHOT 2: Second measurement
    sub HorizontalAngleValue HorizontalAngleValue 4   # -4° H (back to middle)
    add VerticalAngleValue VerticalAngleValue 4       # +4° V (up more)
    min VerticalAngleValue VerticalAngleValue 90      # Clamp at V=90°
    j Shot3                             # Go to third shot
    
UseSmallOffsets:
    # SMALL OFFSETS (for V > 70°) - Creates proper triangle: 6°H × 5°V spread
    # Pattern: Shot 1 (H+1,V), Shot 2 (H+7,V+2.5), Shot 3 (H+4,V+5)
    # Scaled down to avoid V=90° overshoot while maintaining good geometry
    add HorizontalAngleValue HorizontalAngleValue 6   # +6° H (right)
    add VerticalAngleValue VerticalAngleValue 2.5     # +2.5° V (up)
    min VerticalAngleValue VerticalAngleValue 90      # Clamp at V=90°
    s MedSatRefID Horizontal HorizontalAngleValue
    s MedSatRefID Vertical VerticalAngleValue
    jal DishMovementYield               # Wait for movement
    sleep 25                            # Wait for signal stabilization
    jal DataGatherThreeShot             # SHOT 2: Second measurement
    sub HorizontalAngleValue HorizontalAngleValue 3   # -3° H (back to middle)
    add VerticalAngleValue VerticalAngleValue 2.5     # +2.5° V (up more)
    min VerticalAngleValue VerticalAngleValue 90      # Clamp at V=90°
    
Shot3:
    s MedSatRefID Horizontal HorizontalAngleValue     # Set position for shot 3
    s MedSatRefID Vertical VerticalAngleValue
    jal DishMovementYield               # Wait for movement
    sleep 25                            # Wait for signal stabilization
    jal DataGatherThreeShot             # SHOT 3: Third measurement


# ============================================================================
# WAIT FOR SOLVER AND POINT TO TRUE LOCATION
# ============================================================================
SetTrueTraderLocation:
    get r12 db 12                       # Check if solver finished
    beqz r12 SetTrueTraderLocation      # Wait for solver to complete
    get FinalHorizontalAngleValue db 43 # Get calculated H angle from solver
    get FinalVerticalAngleValue db 44   # Get calculated V angle from solver
    s MedSatRefID Horizontal FinalHorizontalAngleValue  # Point to exact location
    s MedSatRefID Vertical FinalVerticalAngleValue
    put db 12 0                         # Reset trigger
    yield                               # Yield
    lb r0 HASH("StructureLogicButton") Activate Maximum
    put db 12 r0                       # Store button state
    get r12 db 12                       # Check for Reset Button Press
    breqz r12 -3                        # Loop if not complete
    j ResetDish                         # Done - reset for next search

    
# ============================================================================
# UPDATE CURRENT STRONGEST SIGNAL
# Reads trader data from dish and extracts tier information
# ============================================================================
UpdateCurrentStrongestSignal:
    put MedSatRefID 0 TraderInstruction.WriteTraderData  # Request trader data
    get CurrentTraderLock MedSatRefID 1  # Read trader metadata (packed format)
    put db 1 CurrentTraderLock          # Store full metadata to IC housing
    srl CurrentTraderLock CurrentTraderLock 16  # Shift right 16 bits
    and CurrentTraderLock $FF CurrentTraderLock # Mask to get byte 3 (tier)
    sbn SmallLED HASH("CTL") Setting CurrentTraderLock  # Display tier on LED
    j ra                                # Return from subroutine

# ============================================================================
# DISH MOVEMENT YIELD
# Waits until dish finishes moving to commanded position
# ============================================================================
DishMovementYield:
    yield                               # Allow other processes
    l r11 MedSatRefID Idle              # Check if dish is idle (done moving)
    beqz r11 DishMovementYield          # Loop until idle
    j ra                                # Return from subroutine

# ============================================================================
# ITERATE FORWARD
# If contact lost during triangulation, move forward and retry
# ============================================================================
IterateForward:
    add HorizontalAngleValue 5 HorizontalAngleValue   # Move +5° H
    add VerticalAngleValue 2.5 VerticalAngleValue     # Move +2.5° V
    j InitThreeShotProcedure            # Try triangulation again

# ============================================================================
# DATA GATHER THREE SHOT
# Sends measurement data to solver chips
# Sends: WattsReachingContact, Vertical angle, Horizontal angle
# ============================================================================
DataGatherThreeShot:
    l r13 MedSatRefID WattsReachingContact  # Read signal strength (watts)
    lbn r14 2037291645 HASH("TrinityAngleSolver") ReferenceId 1  # Load angle solver
    put r14 1 r13                       # Send watts to angle solver
    put r14 12 1                        # Trigger angle solver to process
    l r13 MedSatRefID Vertical          # Read current V angle
    lbn r14 2037291645 HASH("TrinityPolynomial") ReferenceId 1  # Load polynomial corrector
    put r14 0 r13                       # Send V angle for correction (dish offset)
    l r13 MedSatRefID Horizontal        # Read current H angle
    put r14 5 r13                       # Send H angle to polynomial corrector
    put r14 12 1                        # Trigger polynomial corrector
    j ra                                # Return from subroutine
