# ============================================================================
# TRINITY TRADER TRIANGULATOR v1.9
# Main search and triangulation controller
# Uses 3-shot measurement pattern for accurate trader location
# Changelog v1.9:
# - Simplified triangulation to use single 6°H × 5°V triangle pattern for all angles
# - Removed adaptive offset branching (was: 8°×8° for V≤70°, 6°×5° for V>70°)
# - New pattern: Shot 1 (H+1,V), Shot 2 (H+7,V+2.5), Shot 3 (H+4,V+5)
# - Triangle stays to the left of initial detection (matches spiral scan geometry)
# - Prevents V=90° overshoot while maintaining good triangulation geometry
# - Reduced code complexity without sacrificing accuracy
# - Adjuste sleep time from 25 to a wait for resolve loop
#ignorelimits
# Changelog v1.8:
# - Used smaller V increment (0.055°) for more precise spiral scanning
# - Helps locate traders that might be missed with larger increments
# ============================================================================



Init_Preload:
# ============================================================================
# REGISTER ALIASES - Make code readable
# ============================================================================
alias TempStore r0                      # Temporary value storage
alias SelectedTrader r1                 # Trader tier to search for (0=Close, 1=Med, 2=Far)
alias CurrentTraderLock r2              # Currently detected trader tier from dish
alias HorizontalAngleValue r3           # Current horizontal angle (0-360°)
alias VerticalAngleValue r4             # Current vertical angle (0-90°)
alias TraderID r5                       # Trader contact ID (unused in this version)
alias FinalHorizontalAngleValue r6      # Final calculated H angle from solver
alias FinalVerticalAngleValue r7        # Final calculated V angle from solver
alias SmallSatRefID r9                  # Small satellite dish reference (unused)
alias MedSatRefID r10                   # Medium satellite dish reference
alias LargeSatRefID r11                 # Large satellite dish reference (unused)
alias TrinityOS r12
alias AngleSolver r13
alias PolynomialSolver r14

# ============================================================================
# DEVICE DEFINITIONS - Hash names for devices
# ============================================================================
move SelectedTrader 2                   # Default to Far tier (2) traders
define MedSat HASH("StructureSatelliteDish")
define LargeSat HASH("StructureLargeSatelliteDish")
define TraderSelectDial HASH("StructureLogicDial")
define StartButton HASH("StructureLogicButton")
define SmallLED HASH("StructureConsoleLED5")

# ============================================================================
# Loading RefIDs for devices - connect to IC10 Housings
# ============================================================================
lbn TrinityOS 2037291645 HASH("TrinityOS") ReferenceId 1
lbn AngleSolver 2037291645 HASH("TrinityAngleSolver") ReferenceId 1
lbn PolynomialSolver 2037291645 HASH("TrinityPolynomial") ReferenceId 1
# ============================================================================
# INITIAL SETUP - Configure devices
# ============================================================================
sb TraderSelectDial Mode 2              # Set dial to mode 2 (0-2 range for trader tiers)
lb MedSatRefID MedSat ReferenceId Minimum   # Load medium dish reference
lb LargeSatRefID LargeSat ReferenceId Minimum  # Load large dish reference
sbn SmallLED HASH("CTL") Mode 0         # Configure LED display
sbn SmallLED HASH("CTL") Setting 0      # Set LED to 0 initially
sbn SmallLED HASH("CTID") Mode 0        # Configure LED for trader ID display

# ============================================================================
# RESET DISH - Return to starting position
# ============================================================================
ResetDish:
    put db 12 0
    s MedSatRefID Horizontal 0          # Point to H=0° (north)
    s MedSatRefID Vertical 0            # Point to V=0° (horizon)
    s MedSatRefID Setting 500           # Set power to 500W for scanning
    s MedSatRefID BestContactFilter -1  # Disable contact filtering (see all traders)
    move HorizontalAngleValue 0         # Reset H tracking variable
    move VerticalAngleValue 0           # Reset V tracking variable
jal DishMovementYield                   # Wait for dish to reach position
put MedSatRefID 0 TraderInstruction.WriteTraderData
# ============================================================================
# WAIT FOR START BUTTON
# ============================================================================
s MedSatRefID On 0
s LargeSatRefID On 0
WaitForInit:
lb TempStore StartButton Activate Maximum  # Check if start button pressed
beq TempStore 0 WaitForInit             # Loop until button pressed
lb SelectedTrader TraderSelectDial Setting Maximum
put TrinityOS 511 6 
yield
WaitForInit2:
lb TempStore StartButton Activate Maximum  # Check if start button pressed
beq TempStore 0 WaitForInit2           # Loop until button pressed

# ============================================================================
# SPIRAL SCAN - Search for trader using Archimedean spiral pattern
# Scans from V=0° to V=90° in 0.9° H × 0.055° V increments
# ============================================================================
put TrinityOS 511 7
s MedSatRefID On 1
s LargeSatRefID On 1
StartSpiralScan:
    yield                               # Allow other processes to run
    s MedSatRefID Horizontal HorizontalAngleValue  # Set dish H position
    s MedSatRefID Vertical VerticalAngleValue
    s LargeSatRefID Horizontal HorizontalAngleValue  # Set dish H position
    s LargeSatRefID Vertical VerticalAngleValue       # Set dish V position
    jal UpdateCurrentStrongestSignal    # Check for trader signal
    beq CurrentTraderLock SelectedTrader InitThreeShotProcedure  # Found it!
    add HorizontalAngleValue 0.9 HorizontalAngleValue  # Increment H by 0.9°
    add VerticalAngleValue 0.055 VerticalAngleValue  # Increment V by 0.055°
    blt VerticalAngleValue 90 StartSpiralScan  # Continue if V < 90°
    j ResetDish                         # Reached top, reset and start over

# ============================================================================
# THREE-SHOT TRIANGULATION PROCEDURE
# Takes 3 measurements in a triangle pattern to calculate exact position
# Pattern: Shot 1 (H+1,V), Shot 2 (H+7,V+2.5), Shot 3 (H+4,V+5)
# Creates 6°H × 5°V triangle - safe at all angles, good geometry
# ============================================================================
InitThreeShotProcedure:
    put TrinityOS 511 8
    s MedSatRefID Setting 7500          # Increase power to 7500W for precise measurements
    add HorizontalAngleValue HorizontalAngleValue 1  # Move +1° H from lock position
    s MedSatRefID Horizontal HorizontalAngleValue    # Apply position
    jal DishMovementYield               # Wait for dish movement
    jal WaitForResolve          # Wait for signal to stabilize
    l TempStore MedSatRefID WattsReachingContact  # Check if still in contact
    beq TempStore -1 IterateForward           # Lost contact? Iterate forward
    jal DataGatherThreeShot             # SHOT 1: Gather first measurement
    
    # Optimized triangle pattern for all angles
    # Shot 1: (H+1, V) - near initial detection
    # Shot 2: (H+21, V+2.5) - far left, slightly down
    # Shot 3: (H+11, V-2.5) - middle, back up (forms proper triangle)
    # Creates ~20°H × 5°V triangle with excellent geometry
    add HorizontalAngleValue HorizontalAngleValue 20  # +20° H (move far left)
    add VerticalAngleValue VerticalAngleValue 2.5     # +2.5° V (move down)
    min VerticalAngleValue VerticalAngleValue 90      # Clamp at V=90°
    s MedSatRefID Horizontal HorizontalAngleValue
    s MedSatRefID Vertical VerticalAngleValue
    jal DishMovementYield               # Wait for movemen
    jal WaitForResolve
    jal DataGatherThreeShot             # SHOT 2: Second measurement
    
    sub HorizontalAngleValue HorizontalAngleValue 10  # -10° H (move back right)
    sub VerticalAngleValue VerticalAngleValue 5       # -5° V (move back up)
    max VerticalAngleValue VerticalAngleValue 0       # Clamp at V=0° (zenith)
    s MedSatRefID Horizontal HorizontalAngleValue
    s MedSatRefID Vertical VerticalAngleValue
    jal DishMovementYield               # Wait for movement
    jal WaitForResolve          # Wait for signal stabilization
    jal DataGatherThreeShot             # SHOT 3: Third measurement


# ============================================================================
# WAIT FOR SOLVER AND POINT TO TRUE LOCATION
# ============================================================================
SetTrueTraderLocation:
    put TrinityOS 511 10
    get TempStore db 12                       # Check if solver finished
    beqz TempStore SetTrueTraderLocation  
    put db 12 0         # Wait for solver to complete
    get FinalHorizontalAngleValue db 43 # Get calculated H angle from solver
    get FinalVerticalAngleValue db 44   # Get calculated V angle from solver
    yield
    s MedSatRefID Horizontal FinalHorizontalAngleValue  # Point to exact location
    s MedSatRefID Vertical FinalVerticalAngleValue
    s LargeSatRefID Horizontal FinalHorizontalAngleValue  # Point to exact location
    s LargeSatRefID Vertical FinalVerticalAngleValue
    l TempStore MedSatRefID SignalID
    s LargeSatRefID BestContactFilter TempStore
    put TrinityOS 511 11 #Tell OS that it can take over interrogation now
    yield   
    put db 12 0                      
    get TempStore db 12                
    breqz TempStore -2                      
    j ResetDish                         

    
# ============================================================================
# UPDATE CURRENT STRONGEST SIGNAL
# Reads trader data from dish and extracts tier information
# ============================================================================
UpdateCurrentStrongestSignal:
    put MedSatRefID 0 TraderInstruction.WriteTraderData  # Request trader data
    get CurrentTraderLock MedSatRefID 1  # Read trader metadata (packed format)
    put db 1 CurrentTraderLock          # Store full metadata to IC housing
    srl CurrentTraderLock CurrentTraderLock 16  # Shift left 16 bits
    and CurrentTraderLock $FF CurrentTraderLock # Mask to get byte 3 (tier)
    sbn SmallLED HASH("CTL") Setting CurrentTraderLock  # Display tier on LED
    j ra                                # Return from subroutine

# ============================================================================
# DISH MOVEMENT YIELD
# Waits until dish finishes moving to commanded position
# ============================================================================
DishMovementYield:
    yield                               # Allow other processes
    l r0 MedSatRefID Idle              # Check if dish is idle (done moving)
    beqz r0 DishMovementYield          # Loop until idle
    j ra                                # Return from subroutine

# ============================================================================
# ITERATE FORWARD
# If contact lost during triangulation, move forward and retry
# ============================================================================
IterateForward:
    add HorizontalAngleValue 5 HorizontalAngleValue   # Move +5° H
    add VerticalAngleValue 2.5 VerticalAngleValue     # Move +2.5° V
    j InitThreeShotProcedure            # Try triangulation again

# ============================================================================
# DATA GATHER THREE SHOT
# Sends measurement data to solver chips
# Sends: WattsReachingContact, Vertical angle, Horizontal angle
# ============================================================================
DataGatherThreeShot:
    put TrinityOS 511 9
    l r15 MedSatRefID WattsReachingContact  # Read signal strength (watts)
    put AngleSolver 1 r15                       # Send watts to angle solver
    put AngleSolver 12 1                        # Trigger angle solver to process
    l r15 MedSatRefID Vertical          # Read current V angle
    put PolynomialSolver 0 r15                       # Send V angle for correction (dish offset)
    l r15 MedSatRefID Horizontal        # Read current H angle
    put PolynomialSolver 5 r15                       # Send H angle to polynomial corrector
    put PolynomialSolver 12 1                        # Trigger polynomial corrector
    j ra                                # Return from subroutine
# ============================================================================
# WAIT FOR RESOLVE
# Waits for the trader signal to be resolved  (the green bar to be filled)
# Sends the script back to where it was
# ============================================================================
WaitForResolve:
yield
l TempStore MedSatRefID SignalStrength
bltz TempStore WaitForResolve 
j ra